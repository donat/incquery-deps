\appendix
\chapter{JAPC example source code}
\label{examplesource}

\begin{lstlisting}[caption=JAPC classes (package name: cern.japc)]

public interface Parameter {
    public ParameterValue getValue();
    public void setValue(ParameterValue value);
}
public class ParameterFactory {
    public static ParameterFactory newInstance() {
        return null;
    }
    public Parameter newParameter(String parameterName) {
        return null;
    }
}
public interface ParameterValue {	
    public void getString();
}
\end{lstlisting}

\begin{lstlisting}[caption=JAPC extension classes (package name:
cern.japc.ext.cmw)]
public class CmwParameter implements Parameter {
    public @Override ParameterValue getValue() {
        return null;
    }
    public @Override void setValue(ParameterValue value) {
    }
}
public class CmwParameterFactory extends ParameterFactory {
    public CmwParameterFactory() {
        super();
    }
    public @Override Parameter newParameter(String parameterName) {    
        return null;
    }
}
\end{lstlisting}

\begin{lstlisting}[caption=User project classes (package name:
cern.accsoft.gui.alarms)]
public class CommandPanel {
     public void setDeviceName(final String deviceName) {
         Parameter parameter = ParameterFactory.newInstance().newParameter("paramname");
         parameter.setValue(null);
     }
     void handleReset(String deviceName) {
         Parameter parameter = ParameterFactory.newInstance().newParameter("paramname");
         parameter.getValue();
     }
}
\end{lstlisting}

\chapter{Model queries source code}
\label{examplequeries}


%\scriptsize

\begin{lstlisting}
package cern.devtools.deps.query.cp3

import "http://inf.mit.bme.hu/donat/incquery-deps/wsmodel"
import "http://inf.mit.bme.hu/donat/incquery-deps/cp3model"
import "http://www.eclipse.org/emf/2002/Ecore"

//-----------------------------------------------------------------------------
// Helper patterns for composition
//-----------------------------------------------------------------------------

private pattern projectsWithSameName(repoProject : CP3Project, wsProject : WProject) = {
	CP3Project.name(repoProject, commonName);
	WProject.name(wsProject, commonName);
}

private pattern methodsWithSameNameAndSameProject(repoMethod : CP3Method, 
 wsMethod : WMethod) = {
	// Find the items contained by the same named projects.
	CP3Project.classes.methods(repoProject, repoMethod);
	WProject.packageFragmentRoots.packageFragments
	   .compilationUnits.types.methods(wsProject, wsMethod);
	find projectsWithSameName(repoProject, wsProject);

	// Join the methods by signature.
	CP3Method.name(repoMethod, name);
	WMethod.name(wsMethod, name);
}

private pattern typesInWsProject(wsType : WType, wsProject: WProject) = {
	WProject.packageFragmentRoots.packageFragments
	   .compilationUnits.types(wsProject, wsType);
}

private pattern methodsInWsType(wsMethod : WMethod, wsType : WType) = {
	WType.methods(wsType, wsMethod);	
}

private pattern fieldsInWsType(wsField: WField, wsType : WType) = {
	WType.fields(wsType, wsField);	
}

private pattern classNamesInRepoProjects(repoProject, repoClassName) = {
	CP3Project(repoProject);
	CP3Project.classes.name(repoProject, repoClassName);
}

private pattern classNamesInWorkspaceProjects(wsProject, simpleName) = {
	WProject.packageFragmentRoots.packageFragments
	    .compilationUnits.types.name(wsProject, simpleName);
}

private pattern methodNamesInRepoProjects(repoProject, name) = {
	CP3Project(repoProject);
	CP3Project.classes.methods.name(repoProject, name);
}

private pattern methodNamesInWorkspaceProjects(wsProject, name) = {
	WProject(wsProject);
	WProject.packageFragmentRoots.packageFragments
	   .compilationUnits.types.methods.name(wsProject, name);
}

private pattern fieldNamesInWorkspaceProjects(wsProject, name) = {
	WProject(wsProject);
	WProject.packageFragmentRoots.packageFragments
	   .compilationUnits.types.fields.name(wsProject, name);
}

private pattern fieldNamesInRepoProjects(repoProject, name) = {
	CP3Project(repoProject);
	CP3Project.classes.fields.name(repoProject, name);
}

private pattern classesWithSameNameAndSameProject(repoClass : CP3Class, 
  wsClass : WType) = {
	CP3Project.classes(repoProject, repoClass);
	WProject.packageFragmentRoots.packageFragments
	    .compilationUnits.types(wsProject, wsClass);
	find projectsWithSameName(repoProject, wsProject);
	CP3Class.name(repoClass, name);
	WType.name(wsClass, name);
}

private pattern fieldsWithSameNameAndSameProject(repoField : CP3Field, 
  wsField : WField) = {
	// Find the items contained by the same named projects.
	CP3Project.classes.fields(repoProject, repoField);
	WProject.packageFragmentRoots.packageFragments
	    .compilationUnits.types.fields(wsProject, wsField);
	find projectsWithSameName(repoProject, wsProject);

	// Join the methods by signature.
	CP3Field.name(repoField, name);
	WField.name(wsField, name);
}


private pattern classesWithSameFQNAndSameProject(repoClass : CP3Class, 
  wsClass : WType) = {
	// Pattern applies only classes within a project with the same name.
	CP3Project.classes(repoProject, repoClass);
	WProject.packageFragmentRoots.packageFragments
	   .compilationUnits.types(wsProject, wsClass);
	find projectsWithSameName(repoProject, wsProject);

	// Classes must have the same name...
	CP3Class.name(repoClass, commonSimpleName);
	WType.name(wsClass, commonSimpleName);
}

// ----------------------------------------------------------------------------

pattern addedClasses(wsClass: WType) = {
	// Find the following attributes for the selected type: 
	//  package name, simple name, project.
	WType.name(wsClass, simpleName);
	WProject.packageFragmentRoots.packageFragments
	   .compilationUnits.types(wsProject, wsClass);

	// Find the project in the repository.
	find projectsWithSameName(repoProject, wsProject);

	// Check that the repository project does not contain a class with the same name.
	neg find classNamesInRepoProjects(repoProject, simpleName);
}

pattern removedClasses(repoClass: CP3Class) = {
	CP3Project.classes(repoProject, repoClass);
	CP3Class.name(repoClass, name);

	// Find the project in the workspace.
	find projectsWithSameName(repoProject, wsProject);

	// Check that the workspace does not contain a class with the same name.
	neg find classNamesInWorkspaceProjects(wsProject, name);
}

pattern addedMethods(wsMethod: WMethod, wsClass: WType) = {
	// Find the method's project.
	WMethod.name(wsMethod, name);
	WProject.packageFragmentRoots.packageFragments
	   .compilationUnits.types.methods(wsProject, wsMethod);

	//Assign the type to the class.
	WType.methods(wsClass, wsMethod);

	// Find the project in the repository.
	find projectsWithSameName(repoProject, wsProject);

	// Ensure that the method does not exist in the selected repository project.
	neg find methodNamesInRepoProjects(repoProject, name);
}

pattern removedMethod(repoMethod : CP3Method) = {
	// Find the method's project
	CP3Method.name(repoMethod, name);
	CP3Project.classes.methods(repoProject, repoMethod);

	// Find the project in the workspace.
	find projectsWithSameName(repoProject, wsProject);

	// The workspace project is missing.
	neg find methodNamesInWorkspaceProjects(wsProject, name);

}

pattern addedFields(wsField: WField, wsClass: WType) = {
	// Find the method's project.
	WField.name(wsField, name);
	WProject.packageFragmentRoots.packageFragments
	   .compilationUnits.types.fields(wsProject, wsField);

	//Assign the type to the class.
	WType.fields(wsClass, wsField);

	// Find the project in the repository.
	find projectsWithSameName(repoProject, wsProject);

	// Ensure that the method does not exist in the selected repository project.
	neg find fieldNamesInRepoProjects(repoProject, name);
}

pattern removedFields(repoField: CP3Field) = {
	// Find the method's project
	CP3Field.name(repoField, name);
	CP3Project.classes.fields(repoProject, repoField);

	// Find the project in the workspace.
	find projectsWithSameName(repoProject, wsProject);

	// The workspace project is missing.
	neg find fieldNamesInWorkspaceProjects(wsProject, name);

	// Bugfix: remove static initialiser methods.
	//check(! name.contains("<clinit>"));
}

pattern incomingInheritances(repoSource : CP3Class, wsTarget : WType) = {
	// Find equal classes in the two models. 
	find classesWithSameFQNAndSameProject(repoTarget, wsTarget);

	// Join on dependencies.
	CP3Dep.from(dependency, repoSource);
	CP3Dep.to(dependency, repoTarget);

	// Select only the inheritance dependencies.
	//CP3Dep.type(dependency, 4);
	CP3Dep.type(dependency, type);
	
	// Dependency type: inheritance.
	check (type == 5);
}

pattern incomingClassUsages(repoSource : CP3Class, wsTarget : WType) = {
	// Find equal classes in the two models. 
	find classesWithSameFQNAndSameProject(repoTarget, wsTarget);

	// Join on dependencies.
	CP3Dep.from(dependency, repoSource);
	CP3Dep.to(dependency, repoTarget);

	// Select only the classes usage dependencies.
	CP3Dep.type(dependency, type);
	
	// Dependency type: method usage.
	check (type == 4);
}

pattern incomingMethodCalls(repoSource : CP3Method, wsTarget : WMethod) = {
	// Join the methods.
	find methodsWithSameNameAndSameProject(repoTarget, wsTarget);

	// Join on dependencies.
	CP3Dep.from(dependency, repoSource);
	CP3Dep.to(dependency, repoTarget);

	// Select only the classes usage dependencies.
	CP3Dep.type(dependency, type);
	
	// 1. method call
	check (type == 1);
}

pattern incomingMethodOverride(repoSource : CP3Method, wsTarget : WMethod) = {
	// Join the methods.
	find methodsWithSameNameAndSameProject(repoTarget, wsTarget);

	// Join on dependencies.
	CP3Dep.from(dependency, repoSource);
	CP3Dep.to(dependency, repoTarget);

	// Select only the classes usage dependencies.
	CP3Dep.type(dependency, type);
	
	check (type == 1);
}

pattern incomingFieldAccesses(repoSource : CP3Method, wsTarget : WField) = {
	// Join the fields.
	find fieldsWithSameNameAndSameProject(repoTarget, wsTarget);

	// Join on dependencies.
	CP3Dep.from(dependency, repoSource);
	CP3Dep.to(dependency, repoTarget);

	// Select only the classes usage dependencies.
	CP3Dep.type(dependency, type);
	check (type == 3);
}

pattern impactCausedByRemovedFields(repoFrom : CP3Method, 
  repoTo : CP3Field, type : EShort) = {
	find removedFields(repoTo);
	CP3Dep.from(d, repoFrom);
	CP3Dep.to(d, repoTo);
	CP3Dep.type(d, type);
	check(type == 4);
}

pattern impactCausedByRemovedMethods(repoFrom : CP3Method, 
  repoTo : CP3Method, type : EShort) = {
	// method call
	find removedMethod(repoTo);
	CP3Dep.from(d, repoFrom);
	CP3Dep.to(d, repoTo);
	CP3Dep.type(d, type);
	check (type == 1);
} or {
	// or method override
	find removedMethod(repoTo);
	CP3Dep.from(d, repoFrom);
	CP3Dep.to(d, repoTo);
	CP3Dep.type(d, type);
	check (type == 3);
}

pattern impactCausedByRemovedClasses(repoFrom : CP3Class, 
  repoTo : CP3Class, type : EShort) = {
	// class usage.
	find removedClasses(repoTo);
	CP3Dep.from(d, repoFrom);
	CP3Dep.to(d, repoTo);
	CP3Dep.type(d, type);
	check (type == 3);
} or {
	// or inheritance
	find removedClasses(repoTo);
	CP3Dep.from(d, repoFrom);
	CP3Dep.to(d, repoTo);
	CP3Dep.type(d, type);
	check (type == 3);
}

pattern outgoingMethodCallDependencyAdded(wsMethodFrom: WMethod, 
  wsMethodTo: WMethod) = {	
	// Finds method call dependencies between projects.
	WDependency.from(d,wsMethodFrom);
	WDependency.to(d, wsMethodTo);
	WDependency.type(d,::METHOD_CALL);
	WProject.packageFragmentRoots.packageFragments
	  .compilationUnits.types.methods(wsP1, wsMethodFrom);
	WProject.packageFragmentRoots.packageFragments
	  .compilationUnits.types.methods(wsP2, wsMethodFrom);
	check (!wsP1.equals(wsP2));
	
	// The elements exist in the repository
	find methodsWithSameNameAndSameProject(repoMethodFrom, wsMethodFrom);
	find methodsWithSameNameAndSameProject(repoMethodTo, wsMethodTo);
	
	// But the dependency does not exist in the workspace
	CP3Dep.type(repoD, type);
	check(type == 3);
	neg find repoDependency(repoMethodFrom, repoMethodTo, repoD);
	
}


private pattern repoDependency(ceFrom: CP3AbstractItem, 
  ceTo: CP3AbstractItem, d : CP3Dep) = {
	CP3Dep.from(d, ceFrom);
	CP3Dep.to(d, ceTo);
}

\end{lstlisting}
